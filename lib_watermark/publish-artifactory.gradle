apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    excludes = ['**/*.kt']

}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    if (project.getPlugins().hasPlugin('com.android.application') ||
            project.getPlugins().hasPlugin('com.android.library')) {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.clistery.app'
            version = VERSION_NAME
            artifactId 'watermark'
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            artifact source: androidSourcesJar, classifier: 'sources', extension: 'jar'
            artifact source: androidJavadocsJar, classifier: 'javadoc', extension: 'jar'
        }
    }
}

artifactory {
    contextUrl = artifactory_url
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = artifactory_maven_name
            username = artifactory_maven_user
            password = artifactory_maven_publish_key
            maven = true
        }
        defaults {
            publications('maven')
            publishArtifacts = true
            publishPom = true
            maven = true
        }
    }
}